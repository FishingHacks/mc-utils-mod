plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}


repositories {
    maven { url = 'https://maven.skye.vg/' }
}

dependencies {
    //    localRuntime "link.e4mc:e4mc_minecraft-neoforge:5.3.1"
    compileOnly "link.e4mc:e4mc_minecraft-neoforge:5.3.1"
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
            programArgument '--nogui'
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }